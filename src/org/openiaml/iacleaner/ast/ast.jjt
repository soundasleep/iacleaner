
options {
  MULTI=true;
  VISITOR=true;
  // NODE_DEFAULT_VOID=true; /* makes all nodes #void by default, reducing the number of elements generated */
}

PARSER_BEGIN(InternetApplication)

package org.openiaml.iacleaner.ast;

import java.util.*;
import java.io.*;

/** A grammar for web applications, maybe? */
public class InternetApplication {

  public static InternetApplication instance = null;
  
  /**
   * For loading test cases programatically.
   */
  public static SimpleNode loadFile(File f, int initialState) throws ParseException, FileNotFoundException {
  	FileInputStream fis = new FileInputStream(f);
  	if (instance == null) {
  		instance = new InternetApplication(fis);
  	} else {
  		ReInit(fis);
  	}
  	InternetApplicationTokenManager.SwitchTo(initialState);
  	SimpleNode n;
  	if (initialState == JS_STATE) {
  		//System.err.println("JS_STATE");
 		n = StartJs();
  	} else {
  		//System.err.println("HTML_STATE");
  		n = Start();
  	}
  	return n;
  }

  /**
   * For loading test cases programatically.
   * Works out the initial state based on the file extension of the given
   * file.
   */
  public static SimpleNode loadFile(File f) throws ParseException, FileNotFoundException {
	  String extension = f.getAbsolutePath().substring(f.getAbsolutePath().lastIndexOf("."));
	  
	  int initialState = 0;
	  if (extension.equals(".html") || extension.equals(".php")) {
	  	initialState = HTML_STATE;
	  } else if (extension.equals(".js")) {
	  	initialState = JS_STATE;
	  } else {
	  	throw new RuntimeException("Unknown file type: " + extension + " (from file: " + f + ")");
	  }
	  
	  return loadFile(f, initialState);
  }

  /**
   * Loading string sources.
   * 
   * @throws UnsupportedEncodingException if we have a problem with the charset encoding 
   */
  public static SimpleNode loadString(String s, String charset, int initialState) throws ParseException, FileNotFoundException, UnsupportedEncodingException {
	  InputStream fis = new ByteArrayInputStream(s.getBytes(charset));
 	if (instance == null) {
  		instance = new InternetApplication(fis);
  	} else {
  		ReInit(fis);
  	}
  	InternetApplicationTokenManager.SwitchTo(initialState);
  	SimpleNode n;
  	if (initialState == JS_STATE) {
 		n = StartJs();
  	} else {
  		n = Start();
  	}
  	return n;
  }

  /** Loads string sources with a default state of HTML. */
  public static SimpleNode loadString(String s, String charset) throws ParseException, FileNotFoundException, UnsupportedEncodingException {
  	return loadString(s, charset, HTML_STATE);
  }
  
}

PARSER_END(InternetApplication)

<HTML_STATE>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<HTML_TAG_STATE>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<HTML_OPENING_TAG>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<JS_STATE>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<DEFAULT>
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "/*" : PHP_WITHIN_COMMENT
| "//" : PHP_WITHIN_COMMENT_SINGLE
}

/* order also matters: by placing this before the matchall, the comments will be properly ended. */
<PHP_WITHIN_COMMENT>
SKIP:
{
	< "*/" > : DEFAULT
}

<PHP_WITHIN_COMMENT>
MORE:
{
	< ~[] >
}

/* order also matters: by placing this before the matchall, the comments will be properly ended. */
<PHP_WITHIN_COMMENT_SINGLE>
SKIP:
{
	< ["\r","\n"] > : DEFAULT
}

<PHP_WITHIN_COMMENT_SINGLE>
MORE:
{
	< ~["\r","\n"] >
}

/* php reserved words */

TOKEN :
{
	< ECHO_STATEMENT: "echo" >
}

TOKEN :
{
	< FUNCTION_STATEMENT: "function" >
}

TOKEN :
{
	< RETURN_STATEMENT: "return" >
}

TOKEN :
{
	< GLOBAL_STATEMENT: "global" >
}

TOKEN :
{
	< NEW_STATEMENT: "new" >
}

TOKEN :
{
	< OR_STATEMENT: "or" >
}

TOKEN :
{
	< VAR_STATEMENT: "var" >
}

TOKEN :
{
	< CLASS_STATEMENT: "class" >
}

TOKEN :
{
	< INTERFACE_STATEMENT: "interface" >
}

TOKEN :
{
	< IMPLEMENTS_STATEMENT: "implements" >
}

TOKEN :
{
	< EXTENDS_STATEMENT: "extends" >
}

TOKEN :
{
	< ACCESS_MODIFIER: "public" | "private" | "protected" >
}

TOKEN :
{
	< PHP_TRY_STATEMENT: "try" >
}

TOKEN :
{
	< PHP_CATCH_STATEMENT: "catch" >
}

TOKEN :
{
	< PHP_THROW_STATEMENT: "throw" >
}

TOKEN :
{
	< PHP_FINALLY_STATEMENT: "finally" >
}

TOKEN :
{
	< PHP_DIE_STATEMENT: "die" >
}

TOKEN :
{
	< PHP_EXIT_STATEMENT: "exit" >
}

TOKEN : /* if statement */
{
	< PHP_IF_STATEMENT:
		"if" >
}

TOKEN : /* else statement */
{
	< PHP_ELSE_STATEMENT:
		"else" >
}

TOKEN : /* STRING */
{
	< STRING:
		"\"" ("\\\"" | (~["\""]))* "\"" >
}

TOKEN : /* STRING (single quoted) */
{
	< STRING_SQ:
		"'" ("\\'" | (~["'"]))* "'" >
}

TOKEN : /* Decimal */
{
	< DECIMAL:
		(["0"-"9"])+
		( "." (["0"-"9"]) * )? >	/* can have numbers after the decimal point */
}

TOKEN : /* Hexaecimal */
{
	< HEXADECIMAL:
		"0x" (["0"-"9","a"-"f"])+ >
}

TOKEN : /* built in variables */
{
	< PHP_BUILTIN:
		"true" | "false" | "null" >
}

TOKEN : /* Function name */
{
	< PHP_FUNCTION_NAME:
		["A"-"Z","a"-"z","_"] (["0"-"9","A"-"Z","a"-"z","_"])* >
}

TOKEN : /* variable */
{
	< PHP_VARIABLE:
		"$" 
		["A"-"Z","a"-"z","_"] (["0"-"9","A"-"Z","a"-"z","_"])* >
}

TOKEN :
{
	< PHP_OBJECT_OPERATOR: "::" >
}

TOKEN : /* operator -- includes '=>' for arrays */
{
	< PHP_OPERATOR:
		"+" | "-" | "*" | "/" | "%" | "." | "==" | "!=" | "<" | ">" | "<=" | ">=" | "->" | "=>" >
}

TOKEN : /* prefix operator */
{
	< PHP_PREFIX_OPERATOR:
		"!" >
}

TOKEN :
{
	<PHP_BLOCK_END : 
		"?>" > : HTML_STATE
		
}

<HTML_STATE>
TOKEN :
{
	<PHP_BLOCK_BEGIN : 
		"<?php" > : DEFAULT
}

/** --- html --- */

<HTML_STATE>
TOKEN:
{
	< HTML_SCRIPT_TAG: "<script>" > : JS_STATE
}

<JS_STATE>
TOKEN:
{
	< HTML_SCRIPT_TAG_CLOSE: "</script>" > : HTML_STATE
}

<HTML_STATE>
TOKEN:
{
	< HTML_OPEN_TAG: "<" > : HTML_OPENING_TAG
}

<HTML_TAG_STATE>
TOKEN:
{
	< HTML_CLOSE_TAG: ">" > : HTML_STATE
}

<HTML_STATE>
TOKEN:
{
	< HTML_CLOSING_TAG: "</" > : HTML_OPENING_TAG
}

<HTML_OPENING_TAG>
TOKEN: /* HTML_TAG_NAME */
{
	< HTML_TAG_NAME: (<LETTER>)+ > : HTML_TAG_STATE
|
	< #LETTER: ["A"-"Z","a"-"z"] >
}

<HTML_TAG_STATE>
TOKEN:
{
	< HTML_ATTRIBUTE_NAME: (["A"-"Z","a"-"z"])+ >
}

<HTML_TAG_STATE>
TOKEN :
{
	< HTML_STRING:
		"\"" ("\\\"" | (~["\""]))+ "\"" >
}

<HTML_TAG_STATE>
TOKEN:
{
	< HTML_ATTRIBUTE_EQUALS : "=" >
}

<HTML_STATE>
TOKEN:
{
	< HTML_TEXT_BLOCK:
		(~["<"])+ >
}

<HTML_STATE>
TOKEN:
{
	< HTML_COMMENT_START: "<!--" > : HTML_WITHIN_COMMENT
}

/* order also matters: by placing this before the matchall, the comments will be properly ended. */
<HTML_WITHIN_COMMENT>
SKIP:
{
	< "-->" > : HTML_STATE
}

<HTML_WITHIN_COMMENT>
MORE:
{
	< ~[] >
}

/** --- javascript --- */
<JS_STATE>
TOKEN :
{
	< JS_FUNCTION_STATEMENT: "function" >
}

<JS_STATE>
TOKEN :
{
	< JS_RETURN_STATEMENT: "return" >
}

<JS_STATE>
TOKEN :
{
	< JS_NEW_STATEMENT: "new" >
}

<JS_STATE>
TOKEN :
{
	< JS_VAR_STATEMENT: "var" >
}

<JS_STATE>
TOKEN : /* if statement */
{
	< JS_IF_STATEMENT:
		"if" >
}

/**
 * if we use constants in the rules below, they cannot be reused
 * across different lexical states (I think)
 */
<JS_STATE>
TOKEN : { < JS_BRACKET_OPEN: "(" > }
<JS_STATE>
TOKEN : { < JS_BRACKET_CLOSE: ")" > }
<JS_STATE>
TOKEN : { < JS_BRACE_OPEN: "{" > }
<JS_STATE>
TOKEN : { < JS_BRACE_CLOSE: "}" > }
<JS_STATE>
TOKEN : { < JS_END: ";" > }
<JS_STATE>
TOKEN : { < JS_EQUAL: "=" > }
<JS_STATE>
TOKEN : { < JS_COMMA: "," > }

<JS_STATE>
TOKEN : /* else statement */
{
	< JS_ELSE_STATEMENT:
		"else" >
}

<JS_STATE>
TOKEN : /* STRING */
{
	< JS_STRING:
		"\"" ("\\\"" | (~["\""]))* "\"" >
}

<JS_STATE>
TOKEN : /* Decimal */
{
	< JS_DECIMAL:
		(["0"-"9"])+
		( "." (["0"-"9"]) * )? >	/* can have numbers after the decimal point */
}

<JS_STATE>
TOKEN : /* Hexaecimal */
{
	< JS_HEXADECIMAL:
		"0x" (["0"-"9","a"-"f"])+ >
}

<JS_STATE>
TOKEN : /* built in variables */
{
	< JS_BUILTIN:
		"true" | "false" | "null" >
}

<JS_STATE>
TOKEN : /* Function name */
{
	< JS_FUNCTION_NAME:
		["A"-"Z","a"-"z","_"] (["0"-"9","A"-"Z","a"-"z","_"])* >
}

<JS_STATE>
TOKEN : /* single line comment */
{
	< JS_SINGLE_COMMENT:
		"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}

<JS_STATE>
TOKEN: /* multi line comment */
{
	< JS_MULTI_COMMENT: "/*" > : JS_WITHIN_COMMENT
}

/* order also matters: by placing this before the matchall, the comments will be properly ended. */
<JS_WITHIN_COMMENT>
SKIP:
{
	< "*/" > : JS_STATE
}

<JS_WITHIN_COMMENT>
MORE:
{
	< ~[] >
}


<JS_STATE>
TOKEN : /* variable */
{
	< JS_VARIABLE:
		["A"-"Z","a"-"z","_"] (["0"-"9","A"-"Z","a"-"z","_"])* >
}

<JS_STATE>
TOKEN : /* operator */
{
	< JS_OPERATOR:
		"+" | "-" | "*" | "/" | "%" | "==" | "!=" | "<" | ">" | "<=" | ">=" >
}

<JS_STATE>
TOKEN : /* operator */
{
	< JS_OBJECT_OPERATOR: "." >
}

/** Main production. */
SimpleNode Start() : { SimpleNode n; }
{
	Block() { n = jjtThis; }
	<EOF>
	{ return n; }
}

SimpleNode StartJs() : { SimpleNode n; }
{
	JsBlock() { n = jjtThis; }
	<EOF>
	{ return n; }
}

void Block() : {}
{
	(
		HtmlBlock()
		| PhpRootBlock()
	)+
}

void PhpRootBlock() : {}
{
	<PHP_BLOCK_BEGIN>
	PhpBlock()
}

void PhpBlock() : {}
{
	( PhpStatement() 
	  | PhpInlineHtmlBlock() )*
}

/** 
 * this is different to a normal HtmlBlock because these are actually in
 * function structures, e.g. if (true) { [html] }
 */
void PhpInlineHtmlBlock() : {}
{
	<PHP_BLOCK_END>
	(HtmlBlock() )*
	(<PHP_BLOCK_BEGIN> | <EOF> )
}

void PhpStatement() : {}
{
	PhpFunctionDefinition() 
	| PhpLanguageStatement() ";"
	| PhpIfStatement()
	| (<ACCESS_MODIFIER>)?
		( PhpClassDefinition() | PhpInterfaceDefinition() ) 
	| PhpTryCatchBlock()
}

void PhpLanguageStatement() : {}
{
	<ECHO_STATEMENT> PhpValuePossiblyWrapped()
	| <PHP_THROW_STATEMENT> PhpValue()
	| <PHP_DIE_STATEMENT> (
			PhpValuePossiblyWrapped()
		)?
	| (PhpFunctionCall() "(" (PhpArgumentList())? ")"
		(PhpOrSomething()) ?)
	| <RETURN_STATEMENT> (PhpValuePossiblyWrapped() (PhpOrSomething()) ?)?
	| <GLOBAL_STATEMENT> PhpVariableList()
	| PhpVariableAssignment()
}

void PhpVariableAssignment() : {}
{
	<PHP_VARIABLE> 
		(<PHP_OBJECT_OPERATOR> (<PHP_VARIABLE> | <PHP_FUNCTION_NAME>))* 
		"=" PhpValuePossiblyWrapped() (<PHP_THROW_STATEMENT> PhpOrSomething()) ?
}

void PhpFunctionCall() : {}
{
	<PHP_FUNCTION_NAME>
	( <PHP_OBJECT_OPERATOR> <PHP_FUNCTION_NAME> )*
}

void PhpOrSomething() : {}
{
	<OR_STATEMENT> <PHP_FUNCTION_NAME> "(" (PhpArgumentList())? ")"
}

void PhpTryCatchBlock() : {}
{
	<PHP_TRY_STATEMENT> "{"
		PhpBlock()
	"}" 
	(<PHP_CATCH_STATEMENT> "(" <PHP_FUNCTION_NAME> <PHP_VARIABLE> ")" "{"
		PhpBlock()
	"}")*
	(<PHP_FINALLY_STATEMENT> "{"
		PhpBlock()
	"}")?
}

void PhpValue() : {}
{
	(<PHP_PREFIX_OPERATOR>) *		/* e.g. !true */
	
	(
	  (<PHP_FUNCTION_NAME> "(" (PhpArgumentList())? ")")
	  | (<NEW_STATEMENT> <PHP_FUNCTION_NAME> "(" (PhpArgumentList())? ")")
	  | (<PHP_VARIABLE>)
	  | (PhpSimpleValue()) 
	)

	( PhpSelectArray()) ?		/* e.g $a[1][2][3] */

}

void PhpValuePossiblyWrapped() : {}
{
	(( "(" PhpValuePossiblyWrapped() ")" ) | PhpValue() )

	(
		/* e.g. "f() + 42" */
		(<PHP_OPERATOR> PhpValuePossiblyWrapped())
		|
		/* e.g. "f() ? 1 : 2" */
		("?" PhpValuePossiblyWrapped() ":" PhpValuePossiblyWrapped()) 
	)?

}

void PhpSimpleValue() : {}
{
	<STRING> | <STRING_SQ> | <DECIMAL> | <HEXADECIMAL> | <PHP_BUILTIN>
}

void PhpSelectArray() : {}
{
	"[" PhpValuePossiblyWrapped() "]" 
	(PhpSelectArray()) ? 
}

/* this has a special case to allow for array(1,2,) */
void PhpArgumentList() : {}
{
	PhpValuePossiblyWrapped() ("," (PhpArgumentList())? )?
}

void PhpVariableList() : {}
{
	<PHP_VARIABLE> ("," PhpVariableList())?
}

void PhpFunctionDefinition() : {}
{
	<FUNCTION_STATEMENT> 
	<PHP_FUNCTION_NAME> "(" (PhpFunctionArgumentList()) ? ")"
	"{"
		PhpBlock()
	"}"
}

void PhpFunctionArgumentList() : {}
{
	<PHP_VARIABLE>
	( "=" PhpSimpleValue() )?
	( "," PhpFunctionArgumentList() )?
}

void PhpIfStatement() : {}
{
	<PHP_IF_STATEMENT> "(" PhpValuePossiblyWrapped() ")" "{" PhpBlock() "}"
	( <PHP_ELSE_STATEMENT> "{" PhpBlock() "}" ) ?
}

void PhpClassDefinition() : {}
{
	<CLASS_STATEMENT> <PHP_FUNCTION_NAME>
	( 
		( <EXTENDS_STATEMENT> <PHP_FUNCTION_NAME>
			( <IMPLEMENTS_STATEMENT> PhpInterfaceList() )?
		)
		| ( <IMPLEMENTS_STATEMENT> PhpInterfaceList() 
			( <EXTENDS_STATEMENT> <PHP_FUNCTION_NAME> )?
		)
	)?
	"{"
		PhpClassBlock()
	"}"		
}

void PhpInterfaceDefinition() : {}
{
	<INTERFACE_STATEMENT> <PHP_FUNCTION_NAME>
	( 
		( <EXTENDS_STATEMENT> PhpInterfaceList() )
	)?
	"{"
		PhpInterfaceBlock()
	"}"		
}

void PhpInterfaceList() : {}
{
	<PHP_FUNCTION_NAME> ("," PhpInterfaceList())?
}

void PhpClassBlock() : {}
{
	(
		(<ACCESS_MODIFIER>)?
		(
			<VAR_STATEMENT> PhpFunctionArgumentList() ";"
			| PhpFunctionDefinition()
		)
	)*
}

void PhpInterfaceBlock() : {}
{
	(
		(<ACCESS_MODIFIER>)?
		(
			<FUNCTION_STATEMENT> <PHP_FUNCTION_NAME> "(" (PhpFunctionArgumentList()) ? ")" ";"
		)
	)*
}

void HtmlBlock() : {}
{
	HtmlScriptTag() |
	HtmlTag() | HtmlClosingTag() | HtmlTextBlock() | HtmlComment()
}

void HtmlScriptTag() : {}
{
	<HTML_SCRIPT_TAG>
	JsBlock()
	<HTML_SCRIPT_TAG_CLOSE>
}

void HtmlTag() #HtmlTag : { Token t; }
{
	<HTML_OPEN_TAG> 
	t = <HTML_TAG_NAME> {
		jjtThis.setName( t.image );
	}
	(HtmlTagAttribute()) *
	<HTML_CLOSE_TAG>
}

void HtmlClosingTag() #HtmlClosingTag : { Token t; }
{
	<HTML_CLOSING_TAG> 
	t = <HTML_TAG_NAME> {
		jjtThis.setName( t.image );
	} 
	<HTML_CLOSE_TAG>
}

void HtmlTagAttribute() #HtmlTagAttribute : { Token t; }
{
	t = <HTML_ATTRIBUTE_NAME> {
		jjtThis.setName( t.image );
	} 
	<HTML_ATTRIBUTE_EQUALS> 
	t = <HTML_STRING> {
		jjtThis.setValue( t.image );
	}
}

void HtmlTextBlock() #HtmlTextBlock : { Token t; }
{
	t = <HTML_TEXT_BLOCK> {
		jjtThis.setText( t.image );
	}
}

void HtmlComment() : {}
{
	<HTML_COMMENT_START>
}

/** javascript stuff */
void JsBlock() : {}
{
	( JsStatement() 
	  | <JS_SINGLE_COMMENT> 
	  | <JS_MULTI_COMMENT> )*
}

void JsStatement() : {}
{
	JsFunctionDefinition() 
	| JsIfStatement()
	| JsLanguageStatement() <JS_END>
}

void JsLanguageStatement() : {}
{
	JsFunctionCall()
	| JsReturnStatement()
	| JsVariableAssignment()
}

void JsFunctionCall() : { Token t; }
{
	t = <JS_FUNCTION_NAME> { jjtThis.setName(t.image); }
		(
		<JS_BRACKET_OPEN> (JsArgumentList())? <JS_BRACKET_CLOSE>
		| <JS_OBJECT_OPERATOR> <JS_FUNCTION_NAME> <JS_BRACKET_OPEN> (JsArgumentList())? <JS_BRACKET_CLOSE>
		)
		( JsChainedOperator() )?
}

void JsReturnStatement() : {}
{
	<JS_RETURN_STATEMENT> (JsValue())?
}

void JsVariableAssignment() : {}
{
	<JS_VARIABLE> <JS_EQUAL> JsValue()
}

void JsChainedOperator() : {}
{
	<JS_OBJECT_OPERATOR> <JS_FUNCTION_NAME> <JS_BRACKET_OPEN> (JsArgumentList())? <JS_BRACKET_CLOSE>
	( JsChainedOperator() )?
}

void JsValue() : {}
{
	(
	  <JS_FUNCTION_NAME> <JS_BRACKET_OPEN> (JsArgumentList())? <JS_BRACKET_CLOSE>
	  | <JS_NEW_STATEMENT> <JS_FUNCTION_NAME> <JS_BRACKET_OPEN> (JsArgumentList())? <JS_BRACKET_CLOSE>
	  | <JS_VARIABLE>
	  | <JS_STRING>
	  | <JS_DECIMAL>
	  | <JS_HEXADECIMAL>
	  | <JS_BUILTIN> )
	
	( <JS_OPERATOR> JsValue() )?			/* e.g. "f() + 42" */
}

void JsSimpleValue() : {}
{
	<JS_STRING> | <JS_DECIMAL> | <JS_HEXADECIMAL> | <JS_BUILTIN>
}

void JsArgumentList() : {}
{
	JsValue() (<JS_COMMA> JsArgumentList())?
}

void JsVariableList() : {}
{
	<JS_VARIABLE> (<JS_COMMA> JsVariableList())?
}

void JsFunctionDefinition() : {}
{
	<JS_FUNCTION_STATEMENT> 
	<JS_FUNCTION_NAME> <JS_BRACKET_OPEN> (JsFunctionArgumentList()) ? <JS_BRACKET_CLOSE>
	<JS_BRACE_OPEN>
		JsBlock()
	<JS_BRACE_CLOSE>
}

void JsFunctionArgumentList() : {}
{
	<JS_VARIABLE>
	( <JS_EQUAL> JsSimpleValue() )?
	( <JS_COMMA> JsFunctionArgumentList() )?
}

void JsIfStatement() : {}
{
	<JS_IF_STATEMENT> <JS_BRACKET_OPEN> JsValue() <JS_BRACKET_CLOSE> <JS_BRACE_OPEN> JsBlock() <JS_BRACE_CLOSE>
	( <JS_ELSE_STATEMENT> <JS_BRACE_OPEN> JsBlock() <JS_BRACE_CLOSE> ) ?
}
